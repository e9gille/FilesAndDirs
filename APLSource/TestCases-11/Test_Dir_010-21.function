 R←Test_Dir_010(stopFlag batchFlag);⎕TRAP;list;parms;buff
⍝ Test the `Dir` method with plenty of attributes in a non-standard sequence and follow←1.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 parms←⎕NS''
 parms.type←3 4 5 1 0 2 6 7 8
 parms.recursive←1
 list←parms ##.FilesAndDirs.Dir ∆Home,'/'
 buff←⍉⊃list

 →PassesIf 22=↑⍴buff
 →PassesIf 7=∪↑¨⍴¨buff[;0]                  ⍝ 3 is timestamp
 :Select #.APLTreeUtils.GetOperatingSystem ⍬
 :Case 'Win'
     →PassesIf∧/0<↑¨⍴¨buff[;1]              ⍝ 4 is Windows SID
 :CaseList 'Lin' 'Mac'
     →PassesIf∧/{↑↑⎕VFI ⍵}¨buff[;1]         ⍝ This is the numeric User ID as a character vector.
 :Else
     .                                      ⍝ Huuh>!
 :EndSelect
 →PassesIf(,⊂,' ')≡∪1↑¨0⍴¨buff[;2]          ⍝ 5 is owner name; character vector
 →PassesIf∧/buff[;3]∊⍳8                     ⍝ 1 is type: integer between 0 and 7
 →PassesIf 0=buff[;3]+.=4                   ⍝      We have one symbolic link but with follow←0 it won't show
 →PassesIf(,⊂,' ')≡∪1↑¨0⍴¨buff[;4]          ⍝ 0 is filename
 →PassesIf 0∧.<(2=buff[;3])⌿buff[;5]        ⍝ 5 is size in bytes: all files are larger than zero bytes
 →PassesIf 0∧.=buff[;6]                     ⍝ 6 is "hidden" flag; we don't have any hidden files
 →PassesIf 0=+/↑¨⍴¨buff[;7]                 ⍝ 7 is target of symbolic link but we have follow←0 here

 R←∆OK
