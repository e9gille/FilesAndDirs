 {(successFlag more)}←{parms}∆StartDyalogInstance wsname;bool;cmd;options;parms2;successFlag
⍝ This starts a second session of Dyalog.
⍝ `wsname` must be the name of the ws to be loaded. Example is "Files/Test_Misc_012".
⍝
⍝ If any parameters and/or options are to be added then these must be specified as
⍝ key/value pairs via the left argument. What is an argument and what is an option is decided
⍝ by a leading "-". An option's value can only be either 0 or 1, otherwise an error is thrown.
⍝ In case of a 0 the option is not passed on, in case of a 1 it is.
⍝
⍝ If `parms` is empty or undefined then it defaults to:
⍝ ~~~
⍝ ('session_file' 'DoesNotExist') ('maxws' '32MB')('-rt' 1)('-x' 0)('RIDE_INIT' '')
⍝ ~~~
⍝
⍝ Examples:
⍝ ~~~
⍝ parms←''
⍝ parms,←⊂'filename' 'foo.txt'
⍝ parms,←⊂'session_file' 'DoesNotExist'
⍝ parms,←⊂'RIDE_INIT' 'SERVE::9999'
⍝ parms,←⊂'maxws' '128M'
⍝ parms,←⊂'-kbd' 0
⍝ parms,←⊂'-x' 1
⍝ ~~~

 parms←{0∊⍴⍵:⍵ ⋄ #.APLTreeUtils.Nest ⍵}{0<⎕NC ⍵:⍎⍵ ⋄ ''}'parms'
 parms,←('session_file' 'DoesNotExist')('maxws' '32MB')('-rt' 1)('-x' 0)
 options←'-'=↑¨↑¨parms
 ⎕SIGNAL/{0∊⍴⍵:''⍬ ⋄ ∧/(↑¨1↓¨⍵)∊0 1:''⍬ ⋄ 'Invalid value for option' 11}options/parms
 :Select #.APLTreeUtils.GetOperatingSystem ⍬
 :Case 'Win'
     wsname←'expand'##.FilesAndDirs.NormalizePath wsname
     cmd←⊂'"',(↑2 ⎕NQ'#' 'GetCommandLineArgs'),'"'
     cmd,←⊂' ',wsname
     parms2←(~options)/parms
     parms2←({(⍵⍳⍵)=⍳⍴⍵}↑¨parms2)/parms2
     cmd,←↑¨{' ',⍺,'="',(⍵~'"'),'"'}/¨parms2     ⍝ The " will be removed by Windows but they are still essential
     parms2←options/parms
     parms2←({(⍵⍳⍵)=⍳⍴⍵}↑¨parms2)/parms2
     cmd,←{' ',(1⊃⍵)/0⊃⍵}¨parms2
     (successFlag more)←2↑#.OS.WinExecute∊cmd
 :CaseList 'Lin' 'Mac'
     wsname←↑,/1 ⎕NPARTS wsname
     wsname←' '⎕R'\\ '⊣wsname   ⍝ Encode blanks
     wsname,←('.dws'≢#.APLTreeUtils.Lowercase ¯4↑wsname)/'.dws'
     cmd←2 ⎕NQ'.' 'GetEnvironment' 'DYALOG'
     cmd,←'/dyalog ',wsname
     parms2←options/parms
     :If ~0∊⍴parms2←({(⍵⍳⍵)=⍳⍴⍵}↑¨parms2)/parms2
         cmd,←∊{' ',(1⊃⍵)/0⊃⍵}¨parms2
     :EndIf
     parms2←(~options)/parms
     :If ~0∊⍴parms2←({(⍵⍳⍵)=⍳⍴⍵}↑¨parms2)/parms2
         cmd←(∊' ',⍨¨{(0⊃⍵),'="',('"'~⍨1⊃⍵),'"'}¨parms2),cmd
     :EndIf
     cmd,←' 1>/dev/null 2>&1'
     cmd,←' &'
     :Trap 11
         more←⎕SH cmd
         successFlag←1
         more←''
     :Else
         successFlag←0
         more←⎕DMX.Message,'; EN=',⍕⎕EN
     :EndTrap
 :Else
     .⍝ Huuh?!
 :EndSelect
